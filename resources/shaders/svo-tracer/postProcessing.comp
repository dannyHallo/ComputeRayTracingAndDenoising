#version 450
#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

#include "../include/svoTracerDescriptorSetLayouts.glsl"

#include "../include/core/color.glsl"
#include "../include/core/packer.glsl"
#include "../include/core/postProcessing.glsl"

#include "../include/projection.glsl"

const uint kDotPixelInnerRadius = 4;
const uint kDotPixelOuterRadius = 5;
const vec4 kDotColor            = vec4(1, 1, 1, 0.4);

ivec2 highResToLowRes(ivec2 highResuvi) {
  const float ratio =
      float(renderInfoUbo.data.lowResSize.x) / float(renderInfoUbo.data.highResSize.x);
  return ivec2(vec2(highResuvi) * ratio);
}

vec3 blendWithDot(vec3 color, ivec2 uvi) {
  ivec2 center = ivec2(renderInfoUbo.data.highResSize) / 2;
  ivec2 diff   = abs(uvi - center);

  // fast bb culling
  if (diff.x > kDotPixelOuterRadius || diff.y > kDotPixelOuterRadius) {
    return color;
  }

  float diffSq = float(dot(diff, diff));

  // border dropoffFac
  float dist = sqrt(diffSq);
  float dotStrength =
      1 - smoothstep(float(kDotPixelInnerRadius), float(kDotPixelOuterRadius), dist);
  dotStrength *= kDotColor.a;
  return mix(color, kDotColor.rgb, dotStrength);
}

vec3 debugInfoOverwrite(ivec2 lowResUvi) {
  if (lowResUvi.x < 0 || lowResUvi.y < 0 || lowResUvi.x >= renderInfoUbo.data.lowResSize.x ||
      lowResUvi.y >= renderInfoUbo.data.lowResSize.y) {
    return vec3(0);
  }
  return imageLoad(octreeVisualizationImage, lowResUvi).rgb;
}

// #define SHADOW_MAP
// #define BLITTED

void main() {
  ivec2 uvi = ivec2(gl_GlobalInvocationID.xy);

#ifdef SHADOW_MAP
  if (any(greaterThanEqual(uvi, ivec2(imageSize(shadowMapImage))))) {
    return;
  }
  float dist = imageLoad(shadowMapImage, uvi).x;
  vec3 col   = vec3(exp(-dist * 0.1));
  imageStore(renderTargetImage, uvi, vec4(col.bgr, 1.0));
  return;
#endif // SHADOW_MAP

#ifdef BLITTED
  if (any(greaterThanEqual(uvi, ivec2(imageSize(blittedImage))))) {
    return;
  }
  vec3 col = unpackRgbe(imageLoad(blittedImage, uvi).x);
  imageStore(renderTargetImage, uvi, vec4(col.bgr, 1));
  return;
#endif // BLITTED

  ivec2 targetSize = imageSize(renderTargetImage);
  if (any(greaterThanEqual(uvi, targetSize))) {
    return;
  }

  vec3 hdrColor = imageLoad(taaImage, uvi).rgb;

  vec3 color = hdrColor;
  color      = jodieReinhardTmo(hdrColor);

  // dithering pattern, to reduce banding
  color += getDitherMask(uvi);

  ivec2 lowResUv = highResToLowRes(uvi);
  color += debugInfoOverwrite(lowResUv);

  // color = blendWithDot(color, uvi);

  imageStore(renderTargetImage, uvi, vec4(color.bgr, 1));
}
